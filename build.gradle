plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.fanmix'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

repositories {
    mavenCentral()
}

ext {
    set('querydslVersion', "5.0.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.projectlombok:lombok:1.18.28'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // H2
    runtimeOnly 'com.h2database:h2'
    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'
    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Swagger
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'
    // AWS S3
    implementation group: 'io.awspring.cloud', name: 'spring-cloud-aws-starter-s3', version: '3.1.1'
    // p6spy
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.1'
    // oauth & google-api-client
    implementation 'com.google.api-client:google-api-client:2.6.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.apis:google-api-services-people:v1-rev20220531-2.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'org.json:json:20220924'

    // jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    testImplementation 'org.springframework.security:spring-security-test'

    testCompileOnly 'org.projectlombok:lombok'  //빠지면 테스트에서 롬복 사용 못함
    testAnnotationProcessor 'org.projectlombok:lombok'   //빠지면 테스트에서 롬복 사용 못함

    // Querydsl
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Spring Batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    testImplementation 'org.springframework.batch:spring-batch-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Q-Class를 생성할 디렉토리 경로 설정
def querydslSrcDir = 'src/main/generated/querydsl/'

// JavaCompile Task를 수행하는 경우 생성될 소스코드의 출력 디렉토리를 querydslSrcDir로 설정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslSrcDir))
}

// 소스 코드로 인식할 디렉토리에 경로에 Q-Class 파일을 추가
// 이렇게 하면 Q-Class가 일반 Java 클래스처럼 취급되어 컴파일과 실행 시 클래스패스에 포함됨
sourceSets {
    main.java.srcDirs += [querydslSrcDir]
}

// clean Task를 수행하는 경우 지정한 디렉토리를 삭제하도록 설정 즉 자동 생성된 Q-Class를 제거
clean {
    delete file(querydslSrcDir)
}
